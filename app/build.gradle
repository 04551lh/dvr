apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion
    defaultConfig {
        applicationId "com.adasplus.dvr_controller"
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def fileName = "markermachine_${defaultConfig.versionName}_${releaseTime()}.apk"
                outputFileName = fileName
            }
        }
    }
}

static def releaseTime() {
    return new Date().format("yyyyMMddHHmmss")
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    testImplementation rootProject.ext.testDeps["junit"]
    implementation project(path: ':base')
    if (!isMoudle.toBoolean()) {
        //激活功能模块
        implementation project(path: ':activate')
        //设置
        implementation project(path: ':settings')
        //设备连接模块
        implementation project(path: ':deviceconnection')
        //文件导出模块
        implementation project(path: ':fileexport')
        //基本信息
        implementation project(path: ':basicinfo')
    }
    annotationProcessor 'com.alibaba:arouter-compiler:1.2.1'



}
